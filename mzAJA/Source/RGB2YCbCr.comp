#version 450
#include "Common.glsl"

layout (local_size_x = 8, local_size_y = 9) in;
layout (binding = 0, rgba16)  uniform readonly   image2D Source;
layout (binding = 1, rgba8ui) uniform writeonly uimage2D Output;

void yuv8(const ivec2 dst, const ivec2 src, const int YOffset)
{
    uvec3 Y0 = SDR_Out_8(imageLoad(Source, src + ivec2(0, YOffset)).xyz);
    uvec3 Y1 = SDR_Out_8(imageLoad(Source, src + ivec2(1, YOffset)).xyz);
    uvec2 C0  = (Y0.yz + Y1.yz) >> 1;
    imageStore(Output, dst, uvec4(C0.x, Y0.x, C0.y, Y1.x));
}

void main_yuv8()
{ 
    const int   Interlaced = int((InterlacedFlags & 3) != 0);
    const int   YOffset    = int((InterlacedFlags & 2) != 0);
    const ivec2 N          = imageSize(Output) / ivec2(gl_NumWorkGroups * gl_WorkGroupSize).xy;
    const ivec2 uv         = ivec2(gl_GlobalInvocationID.xy) * N;
    
    for(int i = 0; i < N.x; ++i)
    {
        for(int j = 0; j < N.y; ++j)
        {
            const ivec2 ij = uv + ivec2(i, j);
            yuv8(ij, ij * ivec2(2, 1 + Interlaced), YOffset);
        }
    }
}

void v210(const ivec2 dst, const ivec2 src, const int YOffset)
{
    uvec4 Y[3];
    for(int k = 0; k < 3; ++k)
    {
        uvec3 Y0 = SDR_Out_10(imageLoad(Source, src + ivec2(2*k+0, YOffset)).xyz);
        uvec3 Y1 = SDR_Out_10(imageLoad(Source, src + ivec2(2*k+1, YOffset)).xyz);
        uvec2 C  = (Y0.yz + Y1.yz) >> 1;
        Y[k] = uvec4(C.x, Y0.x, C.y, Y1.x);
    }
    
    const uvec4 x = uvec4(Y[0].x, Y[0].w, Y[1].z, Y[2].y);
    const uvec4 y = uvec4(Y[0].y, Y[1].x, Y[1].w, Y[2].z);
    const uvec4 z = uvec4(Y[0].z, Y[1].y, Y[2].x, Y[2].w);

    const uvec4 Q[4] = uvec4[4](0xff & x, 0xff & ((x >> 8) | (y << 2)), 0xff & ((y >> 6) | (z << 4)), z >> 4);
    
    for(int k = 0; k < 4; ++k) 
        imageStore(Output, dst + ivec2(k, 0), uvec4(Q[0][k], Q[1][k], Q[2][k], Q[3][k]));
}

void main_v210()
{
    const int   Interlaced = int((InterlacedFlags & 3) != 0);
    const int   YOffset    = int((InterlacedFlags & 2) != 0);
    const ivec2 N          = imageSize(Output) / ivec2(4, 1) /  ivec2(gl_NumWorkGroups * gl_WorkGroupSize).xy;
    const ivec2 uv         = ivec2(gl_GlobalInvocationID.xy) * N;
    
    for(int i = 0; i < N.x; ++i)
    {
        for(int j = 0; j < N.y; ++j)
        {
            const ivec2 ij  = uv + ivec2(i, j);
            v210(ij * ivec2(4, 1), ij * ivec2(6, 1 + Interlaced), YOffset);
        }
    }
}

void main()
{
    if((InterlacedFlags & 4) == 0) main_yuv8();
    else main_v210();
}